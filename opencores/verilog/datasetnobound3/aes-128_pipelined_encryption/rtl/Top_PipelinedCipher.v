/*
Project        : AES
Standard doc.  : FIPS 197
Module name    : Top_AES_PipelinedCipher 
VARIANT: Separate always blocks for delay reg data and valid
Owner          : Amr Salah
*/

`timescale 1 ns/1 ps

(* keep_hierarchy = "yes" *) module Top_PipelinedCipher
#
(
parameter DATA_W = 128,
parameter KEY_L = 128,
parameter NO_ROUNDS = 10
)
(
input clk,
input reset,
input data_valid_in,
input cipherkey_valid_in,
input [KEY_L-1:0] cipher_key,
input [DATA_W-1:0] plain_text,
output valid_out,
output [DATA_W-1:0] cipher_text
);

wire [NO_ROUNDS-1:0] valid_round_key;
wire [NO_ROUNDS-1:0] valid_round_data;
wire [DATA_W-1:0] data_round [0:NO_ROUNDS-1];
wire valid_sub2shift;
wire valid_shift2key;
wire [DATA_W-1:0]data_sub2shift;
wire [DATA_W-1:0]data_shift2key;
wire [(NO_ROUNDS*DATA_W)-1:0] W;

reg[DATA_W-1:0] data_shift2key_delayed;
reg valid_shift2key_delayed;

KeyExpantion #(DATA_W,KEY_L,NO_ROUNDS) U_KEYEXP(clk,reset,cipherkey_valid_in,cipher_key,W,valid_round_key);

AddRoundKey #(DATA_W)U0_ARK(clk,reset,data_valid_in,cipherkey_valid_in,plain_text,cipher_key,valid_round_data[0],data_round[0]);

genvar i;
generate
for(i=0;i<NO_ROUNDS-1;i=i+1) begin : ROUND
 Round #(DATA_W)U_ROUND(clk,reset,valid_round_data[i],valid_round_key[i],data_round[i],W[(NO_ROUNDS-i)*DATA_W-1:(NO_ROUNDS-i-1)*DATA_W],valid_round_data[i+1],data_round[i+1]);
end
endgenerate

SubBytes #(DATA_W) U_SUB (clk,reset,valid_round_data[NO_ROUNDS-1],data_round[NO_ROUNDS-1],valid_sub2shift,data_sub2shift);
ShiftRows #(DATA_W) U_SH (clk,reset,valid_sub2shift,data_sub2shift,valid_shift2key,data_shift2key);
AddRoundKey #(DATA_W) U_KEY (clk,reset,valid_shift2key_delayed,valid_round_key[NO_ROUNDS-1],data_shift2key_delayed,W[DATA_W-1:0],valid_out,cipher_text);

// VARIANT: Split delay register into two separate always blocks
always @(posedge clk or negedge reset)
if(!reset)begin
    data_shift2key_delayed <= 'b0;
end else begin
 if(valid_shift2key)begin
   data_shift2key_delayed <= data_shift2key;
 end
end

always @(posedge clk or negedge reset)
if(!reset)begin
    valid_shift2key_delayed <= 1'b0;
end else begin
   valid_shift2key_delayed <= valid_shift2key;
end

endmodule